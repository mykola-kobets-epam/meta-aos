DESCRIPTION = "AosCore Service Manager"

LICENSE = "Apache-2.0"
LIC_FILES_CHKSUM = "file://LICENSE;md5=86d3f3a95c324c9479bd8986968f4327"

BRANCH = "pkcs11-root-cert"
SRCREV = "${AUTOREV}"

SRC_URI = "git://github.com/mykola-kobets-epam/aos_core_cpp.git;protocol=https;branch=${BRANCH}"

SRC_URI += " \
    file://sm.cfg \
    file://aos-sm.service \
    file://aos-target.conf \
    file://aos-dirs-service.conf \
    file://aos-cm-service.conf \
    file://aos-reboot.service \
"

S = "${WORKDIR}/git"

inherit cmake pkgconfig systemd

SYSTEMD_SERVICE:${PN} = "aos-sm.service"

MIGRATION_SCRIPTS_PATH = "${base_prefix}/usr/share/aos/sm/migration"

FILES:${PN} += " \
    ${sysconfdir} \
    ${systemd_system_unitdir} \
    ${MIGRATION_SCRIPTS_PATH} \
"

DEPENDS = "grpc grpc-native poco protobuf-native systemd curl libnl nftables crun"

do_configure[network] =  "1"

EXTRA_OECMAKE += " \
    -DFETCHCONTENT_FULLY_DISCONNECTED=OFF \
    -DWITH_CM=OFF \
    -DWITH_IAM=OFF \
    -DWITH_MP=OFF \
    -DWITH_SM=ON \
"
OECMAKE_GENERATOR = "Unix Makefiles"

PACKAGECONFIG ??= "openssl"

PACKAGECONFIG[openssl] = "-DWITH_OPENSSL=ON,-DWITH_OPENSSL=OFF,openssl,"
PACKAGECONFIG[mbedtls] = "-DWITH_MBEDTLS=ON,-DWITH_MBEDTLS=OFF,,"

VIRTUAL_RUNC = "${@bb.utils.contains('LAYERSERIES_CORENAMES', 'dunfell', 'virtual/runc', 'virtual-runc', d)}"

RDEPENDS:${PN} += " \
    quota \
    nftables \
    dnsmasq \
    crun \
    aos-target \
"

RDEPENDS:${PN}:append:aos-secondary-node = " \
    packagegroup-core-nfs-client \
"

RRECOMMENDS:${PN} += " \
    kernel-module-8021q \
    kernel-module-act-mirred \
    kernel-module-bridge \
    kernel-module-cls-matchall \
    kernel-module-ifb \
    kernel-module-nf-conntrack \
    kernel-module-nf-nat \
    kernel-module-nfnetlink \
    kernel-module-nft-chain-nat \
    kernel-module-nft-ct \
    kernel-module-nft-masq \
    kernel-module-nft-nat \
    kernel-module-overlay \
    kernel-module-sch-ingress \
    kernel-module-sch-tbf \
    kernel-module-veth \
    kernel-module-vxlan \
"

do_fetch[vardeps] += " \
    AOS_COMPONENT_RUNTIME_PREFIX \
"

python do_update_config() {
    import json

    file_name = oe.path.join(d.getVar("D"), d.getVar("sysconfdir"), "aos", "sm.cfg")

    with open(file_name) as f:
        data = json.load(f)

    node_hostname = d.getVar("AOS_NODE_HOSTNAME")
    main_node_hostname = d.getVar("AOS_MAIN_NODE_HOSTNAME")

    # Update IAM servers

    data["iamProtectedServerUrl"] = node_hostname + ":8089"
    data["iamPublicServerUrl"] = node_hostname + ":8090"

    # Update CM server

    data["cmServerUrl"] = main_node_hostname + ":8093"

    # Update component prefixes and set container runner

    comp_prefix = d.getVar("AOS_COMPONENT_RUNTIME_PREFIX")

    for runtime in data["runtimes"]:
        isComponent = runtime.get("isComponent", False)

        if isComponent and not runtime["type"].startswith(comp_prefix):
            runtime["type"] = comp_prefix + runtime["type"]

    with open(file_name, "w") as f:
        json.dump(data, f, indent=4)
}

do_install:append() {
    install -d ${D}${sysconfdir}/aos
    install -m 0644 ${WORKDIR}/sm.cfg ${D}${sysconfdir}/aos

    install -d ${D}${systemd_system_unitdir}
    install -m 0644 ${WORKDIR}/aos-sm.service ${D}${systemd_system_unitdir}

    install -d ${D}${sysconfdir}/systemd/system/aos-sm.service.d
    install -m 0644 ${WORKDIR}/aos-dirs-service.conf ${D}${sysconfdir}/systemd/system/aos-sm.service.d/20-aos-dirs-service.conf

    install -d ${D}${sysconfdir}/systemd/system/aos.target.d
    install -m 0644 ${WORKDIR}/aos-target.conf ${D}${sysconfdir}/systemd/system/aos.target.d/${PN}.conf

    install -d ${D}${MIGRATION_SCRIPTS_PATH}
    source_migration_path="/src/sm/database/migration"
    if [ -d ${S}${source_migration_path} ]; then
        install -m 0644 ${S}${source_migration_path}/* ${D}${MIGRATION_SCRIPTS_PATH}
    fi

    install -m 0644 ${WORKDIR}/aos-reboot.service ${D}${systemd_system_unitdir}
}

do_install:append:aos-main-node() {
    install -d ${D}${sysconfdir}/systemd/system/aos-sm.service.d
    install -m 0644 ${WORKDIR}/aos-cm-service.conf ${D}${sysconfdir}/systemd/system/aos-sm.service.d/10-aos-cm-service.conf
}

do_install:append() {
    # Do not install headers files to prevent SDK build conflicts
    rm -rf ${D}${includedir}
}


addtask update_config after do_install before do_package
